{{!
@param IRI   contract
@param IRI   graph
@param float kind-inhibition
@param int   limit
@param int   offset (optional)
}}

{{> templates/prefixes.mustache}}

SELECT ?match (SUM(?contractCount) AS ?score) 
WHERE {
  {
    SELECT ?match (1 AS ?inScoreModifier) (COUNT(?contract) AS ?contractCount)
    WHERE {
      {
        SELECT ?cpvObject
        WHERE {
          GRAPH <{{graph}}> {
            [] pc:lot <{{contract}}> ;
              pc:mainObject/skos:closeMatch ?cpvObject .
          }
        }
      }
      GRAPH <{{graph}}> {
        ?contract pc:mainObject/skos:closeMatch ?cpvObject ;
          pc:lot/pc:awardedTender/pc:bidder ?match . 
      }
    }
    GROUP BY ?match ?cpvObject
  } UNION {
    SELECT ?match ({{kind-inhibition}} AS ?inScoreModifier) (COUNT(?contract) AS ?contractCount)
    WHERE {
      {
        SELECT ?kind
        WHERE {
          GRAPH <{{graph}}> {
            [] pc:lot <{{contract}}> ;
              pc:kind ?kind .
          }
        }
      }
      GRAPH <{{graph}}> {
        ?contract pc:kind ?kind ;
          pc:lot/pc:awardedTender/pc:bidder ?match . 
      }
    }
    GROUP BY ?match ?kind
  }
}
GROUP BY ?match
ORDER BY DESC(?score)
LIMIT {{limit}}
{{#offset}}
OFFSET {{offset}}
{{/offset}}
