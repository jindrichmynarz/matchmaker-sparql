{{!
@param IRI   contract
@param IRI   graph
@param float main-object-inhibition
@param int   limit
@param int   offset (optional)
}}

{{> templates/prefixes.mustache}}

SELECT ?match (if(?_score < 1, ?_score, 1) AS ?score)
WHERE {
  {
    SELECT ?match (COUNT(?contract) * {{main-object-inhibition}} AS ?_score)
    WHERE {
      {
        SELECT ?cpvObject
        WHERE {
          GRAPH <{{graph}}> {
            [] pc:lot <{{contract}}> ;
              pc:mainObject/skos:closeMatch ?cpvObject .
          }
        }
      }
      GRAPH <{{graph}}> {
        ?contract pc:mainObject/skos:closeMatch ?cpvObject ;
          pc:lot/pc:awardedTender/pc:bidder ?match . 
      }
    }
    GROUP BY ?match
  }
}
GROUP BY ?match
ORDER BY DESC(?score)
LIMIT {{limit}}
{{#offset}}
OFFSET {{offset}}
{{/offset}}
