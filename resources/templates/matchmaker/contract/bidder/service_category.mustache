{{!
@param IRI contract
@param IRI graph
@param int limit
@param int offset (optional)
}}

{{> templates/prefixes.mustache}}

SELECT ?match (SUM(?contractCount) AS ?score) 
WHERE {
  {
    SELECT ?match (COUNT(?contract) AS ?contractCount)
    WHERE {
      {
        SELECT ?serviceCategory
        WHERE {
          GRAPH <{{graph}}> {
            [] pc:lot <{{contract}}> ;
              isvz:serviceCategory ?serviceCategory .
          }
        }
      }
      GRAPH <{{graph}}> {
        ?contract isvz:serviceCategory ?serviceCategory ;
          pc:lot/pc:awardedTender/pc:bidder ?match . 
      }
    }
    GROUP BY ?match ?serviceCategory
  }
}
GROUP BY ?match
ORDER BY DESC(?score)
LIMIT {{limit}}
{{#offset}}
OFFSET {{offset}}
{{/offset}}
