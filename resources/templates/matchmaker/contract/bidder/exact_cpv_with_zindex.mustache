{{!
@param IRI contract
@param IRI graph
@param int limit
@param int offset (optional)
}}

{{> templates/prefixes.mustache}}

SELECT ?match
       (SUM(?contractCount * ?zIndex) AS ?score) 
WHERE {
  {
    SELECT ?match ?contractCount (COALESCE(?_zIndex, 0.5) AS ?zIndex)
    WHERE {
      {
        SELECT ?match
               (COUNT(?contract) AS ?contractCount)
               (SAMPLE(?__zIndex) AS ?_zIndex)
        WHERE {
          {
            SELECT ?cpvObject
            WHERE {
              GRAPH <{{graph}}> {
                [] pc:lot <{{contract}}> ;
                  pc:mainObject/skos:closeMatch ?cpvObject .
              }
            }
          }
          GRAPH <{{graph}}> {
            ?contract pc:mainObject/skos:closeMatch ?cpvObject ;
              pc:lot/pc:awardedTender/pc:bidder ?match ;
              pc:contractingAuthority ?contractingAuthority .
          }
          OPTIONAL {
            GRAPH <http://linked.opendata.cz/resource/zindex.cz/dataset> {
              [] dimension:organization ?contractingAuthority ;
                measure:zIndex ?__zIndex .
            }
          }
        }
        GROUP BY ?match ?cpvObject ?contractingAuthority
      }
    }
  }
}
GROUP BY ?match
ORDER BY DESC(?score)
LIMIT {{limit}}
{{#offset}}
OFFSET {{offset}}
{{/offset}}
